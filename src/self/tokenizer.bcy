mod std*;

fn make_tokenizer(file) {
    return {
        file: file,
        line_number: 0,
        start_of_line: 0,
        next_token: nil
    };
}

fn gen_info(tok) {
    return {
        line: tok.line_number,
        col: file::current_position(tok.file) - tok.start_of_line
    };
}

mod token {
    fn keyword(tok, kwd) return { t: "kwd", kwd: kwd, info: gen_info(tok) };
    fn identifier(tok, id) return { t: "id", id: id, info: gen_info(tok) };
    fn operator(tok, op) return { t: "op", op: op, info: gen_info(tok) };
    fn symbol(tok, sym) return { t: "sym", sym: sym, info: gen_info(tok) };
    fn string(tok, s) return { t: "str", val: s, info: gen_info(tok) };
    fn number(tok, num) return { t: "num", val: num, info: gen_info(tok) };
    fn eof(tok) return { t: "eof", info: gen_info(tok) };
}

let __keywords = [ "fn", "let", "loop", "break", "continue", "return", "if", "else", "true", "false", "mod" ];

fn __next_token(tok) {
    if file::eof(tok.file) return token::eof(tok);
    let ch = file::next_char(tok.file);

    loop {
        if ch == "\n" {
            tok.line_number = tok.line_number + 1;
            tok.start_of_line = file::current_position(tok.file);
        };
        if ch < 0 return token::eof(tok);
        if !char::is_whitespace(ch) break;
        ch = file::next_char(tok.file);
    };

    if ch == "{" || ch == "}" || ch == "(" || ch == ")"
        || ch == "[" || ch == "]" || ch == ";" || ch == "," return token::symbol(tok, ch);

    if ch == ":" {
        if file::peek_char(tok.file) == ":" {
            file::next_char(tok.file);
            return token::symbol(tok, "::");
        };
        return token::symbol(":");
    } else if ch == "=" {
        if file::peek_char(tok.file) == ">" {
            file::next_char(tok.file);
            return token::symbol(tok, "=>");
        }
    };

    if char::is_digit(ch) || (ch == "-" && char::is_digit(file::peek_char(tok.file))) {
        let sign = 1;
        if ch == "-" {
            ch = file::next_char(tok.file);
            sign = -1;
        };
        let value = char::digit_value(ch);
        loop {
            if file::eof(tok.file) || !char::is_digit(file::peek_char(tok.file)) break;
            value = value * 10 + char::digit_value(file::next_char(tok.file));
        };
        return token::number(tok, value * sign);
    } else if ch == "\"" {
        let value = "";
        loop {
            if file::eof(tok.file) break;
            ch = file::next_char(tok.file);
            if ch == "\"" break
            else if ch == "\\" {
                let nch = file::next_char(tok.file);
                if nch < 0 { break; }
                else if nch == "\\" { ch = "\\"[0]; }
                else if nch == "n" { ch = "\n"[0]; }
                else if nch == "t" { ch = "\t"[0]; }
                else if nch == "\"" { ch = "\""[0]; }
            };
            str::append(value, ch);
        };
        return token::string(tok, value);
    } else if !char::is_alphanumeric(ch) && ch != "_" {
        let op = "";
        str::append(op, ch);
        loop {
            let pch = file::peek_char(tok.file);
            if file::eof(tok.file) || char::is_alphanumeric(pch) || char::is_whitespace(pch) || pch == ";" break;
            str::append(op, file::next_char(tok.file));
        };
        return token::operator(tok, op);
    } else {
        let id = "";
        str::append(id, ch);
        loop {
            let pch = file::peek_char(tok.file);
            if file::eof(tok.file) || !char::is_alphanumeric(pch) && pch != "_" break;
            str::append(id, file::next_char(tok.file));
        };
        if lists::contains(__keywords, id) {
            return token::keyword(tok, id);
        } else {
            return token::identifier(tok, id);
        }
    }
}

fn next(tok) {
    if tok.next_token != nil {
        let nt = tok.next_token;
        tok.next_token = nil;
        return nt;
    } else {
        return __next_token(tok);
    }
}

fn peek(tok) {
    if tok.next_token == nil {
        tok.next_token = __next_token(tok);
    };
    return tok.next_token;
}

fn _test(path) {
    let tok = make_tokenizer(file::open(path));
    printv(tok);
    loop {
        let t = next(tok);
        if t.t == "eof" break;
        printv(t);
    }
}
